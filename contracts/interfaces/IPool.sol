// SPDX-License-Identifier: AGPL-3.0-only

pragma solidity 0.7.5;
pragma abicoder v2;

import "./IDepositContract.sol";

/**
 * @dev Interface of the Pool contract.
 */
interface IPool {
    /**
    * @dev Event for tracking new pool withdrawal credentials.
    * @param withdrawalCredentials - new withdrawal credentials for the pool validators.
    */
    event WithdrawalCredentialsUpdated(bytes32 withdrawalCredentials);

    /**
    * @dev Event for tracking registered validators.
    * @param publicKey - validator public key.
    * @param operator - address of the validator operator.
    */
    event ValidatorRegistered(bytes publicKey, address operator);

    /**
    * @dev Structure for passing information about new Validator.
    * @param publicKey - BLS public key of the validator, generated by the operator.
    * @param signature - BLS signature of the validator, generated by the operator.
    * @param depositDataRoot - hash tree root of the deposit data, generated by the operator.
    */
    struct Validator {
        bytes publicKey;
        bytes signature;
        bytes32 depositDataRoot;
    }

    /**
    * @dev Constructor for initializing the Pool contract.
    * @param _admin - address of the contract admin.
    * @param _stakedEthToken - address of the StakedEthToken contract.
    * @param _validatorRegistration - address of the VRC (deployed by Ethereum).
    * @param _validators - address of the Validators contract.
    * @param _withdrawalCredentials - withdrawal credentials for the pool validators.
    */
    function initialize(
        address _admin,
        address _stakedEthToken,
        address _validatorRegistration,
        address _validators,
        bytes32 _withdrawalCredentials
    ) external;

    /**
    * @dev Function for retrieving the total collected amount.
    */
    function collectedAmount() external view returns (uint256);

    /**
    * @dev Function for getting the withdrawal credentials used to
    * initiate pool validators withdrawal from the beacon chain.
    */
    function withdrawalCredentials() external view returns (bytes32);

    /**
    * @dev Function for changing withdrawal credentials.
    * @param _withdrawalCredentials - new withdrawal credentials for the pool validators.
    */
    function setWithdrawalCredentials(bytes32 _withdrawalCredentials) external;

    /**
    * @dev Function for retrieving the validator registration contract address.
    */
    function validatorRegistration() external view returns (IDepositContract);

    /**
    * @dev Function for adding deposits to the pool.
    */
    function addDeposit() external payable;

    /**
    * @dev Function for registering new pool validator.
    * @param _validator - validator to register.
    */
    function registerValidator(Validator calldata _validator) external;
}
